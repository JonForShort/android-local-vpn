buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

ext {
    groupId = "com.github.jonforshort"
    artifactId = "android-local-vpn"
    versionCode = 1
    versionName = "0.1." + versionCode
    libraryName = "AndroidLocalVpn"
    libraryDescription = "Local VPN For Android"
}

android {
    compileSdk 32
    ndkVersion "21.1.6352462"

    defaultConfig {
        minSdk 26
        targetSdk 32

        versionCode = versionCode
        versionName = versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    if (rootProject.isProductionBuild()) {
        println("Native debug symbols are not included in build")
    } else {
        packagingOptions {
            jniLibs {
                keepDebugSymbols += ['**/*.so']
            }
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-ktx:1.5.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module = "src/main/rust/vpn"
    libname = "vpn"
    targets = ["arm64", "arm", "x86_64", "x86"]
    profile = rootProject.isProductionBuild() ? "release" : "build"
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = groupId
                artifactId = artifactId
                version = versionName

                from components.release

                pom {
                    name = libraryName
                    description = libraryDescription
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/jonforshort/android-local-vpn"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
